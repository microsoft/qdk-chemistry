// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License. See LICENSE.txt in the project root for
// license information.

/// Shared table and address-space helpers for the SELECT-SWAP loaders.
///
/// These carry no loading strategy of their own: they reshape lookup tables, build the
/// swap network's permutations, and price the measurement-based unlookup. Both the clean
/// loaders in `SelectSwap` and the borrowed-qubit loaders in `SelectSwapDirty` use them.
namespace QDKChemistry.Utils.SelectSwapUtils {

    import Std.Arrays.Chunks;
    import Std.Arrays.Enumerated;
    import Std.Arrays.Flattened;
    import Std.Arrays.MappedOverRange;
    import Std.Arrays.Padded;
    import Std.Arrays.Partitioned;
    import Std.Arrays.Zipped;
    import Std.Canon.ApplyToEachA;
    import Std.Convert.IntAsDouble;
    import Std.Diagnostics.Fact;
    import Std.Math.Ceiling;
    import Std.Math.Lg;
    import Std.TableLookup.Select;
    import QDKChemistry.Utils.UnaryIteration.AddressQubits;
    import QDKChemistry.Utils.UnaryIteration.UnaryIteration;
    import QDKChemistry.Utils.UnaryIteration.UnaryIterationActionIndex;

    /// Zero-pads a lookup table out to the full `2^nRequired` address space.
    internal function PadToAddressSpace(data : Bool[][], nRequired : Int) : Bool[][] {
        Padded(-2^nRequired, [false, size = Length(data[0])], data)
    }

    /// Expands a lookup table to the full `2^nRequired` address space the way `Select` reads it.
    ///
    /// `Select` never reads a surplus address as zero; `UnaryIteration` routes anything at or
    /// above `Length(data)` back onto a real row. A loader that zero-pads instead disagrees with
    /// `Select` -- and with any phase fixup written against `Select` -- on exactly those
    /// addresses. Building the table this way makes the agreement unconditional rather than a
    /// property of which addresses happen to carry amplitude.
    internal function AliasToAddressSpace(data : Bool[][], nRequired : Int) : Bool[][] {
        MappedOverRange(
            i -> data[UnaryIterationActionIndex(Length(data), i)],
            0..2^nRequired - 1
        )
    }

    /// Register slices and the flattened table shared by the swap path and its erasure.
    internal function SwappedLoadShape(
        data : Bool[][][],
        outerAddress : Qubit[],
        innerAddress : Qubit[],
        numSwapBits : Int,
        outerAddressAlwaysValid : Bool,
    ) : (Bool[][], Qubit[], Qubit[]) {
        let nRequired = DimensionsForSelect(data[0], innerAddress);
        Fact(numSwapBits <= nRequired, "Too many bits for SWAP network");
        let m = Length(data[0][0]);
        let k = nRequired - numSwapBits;
        let innerAddressParts = Partitioned([k, numSwapBits], innerAddress[...nRequired - 1]);
        let flatData = FlattenPaddedData(data, nRequired, m, k, outerAddressAlwaysValid);
        let selectAddress = innerAddressParts[0] + outerAddress[...AddressQubits(Length(data)) - 1];
        (flatData, selectAddress, innerAddressParts[1])
    }

    /// Where each chunk ends up after `SwapDataOutputs` runs for a given swap value.
    ///
    /// `result[position]` is the chunk index the butterfly leaves at `position`. Only
    /// `result[0] == swap` is the point of the network; the rest are a permutation that is
    /// *not* `position XOR swap` beyond one swap bit, so it is replayed here rather than
    /// assumed.
    internal function SwapNetworkPermutation(numSwapBits : Int, swap : Int) : Int[] {
        let numChunks = 1 <<< numSwapBits;
        mutable permutation = MappedOverRange(chunk -> chunk, 0..numChunks - 1);
        for bit in 0..numSwapBits - 1 {
            if (swap >>> bit) &&& 1 == 1 {
                let innerStep = 1 <<< bit;
                let outerStep = 1 <<< (bit + 1);
                for pair in 0..numChunks / outerStep - 1 {
                    let low = pair * outerStep;
                    let high = low + innerStep;
                    let held = permutation[low];
                    set permutation w/= low <- permutation[high];
                    set permutation w/= high <- held;
                }
            }
        }
        permutation
    }

    /// The post-butterfly target contents indexed by `select + swap * numSelectStates`.
    internal function SwapPermutedTable(
        flatData : Bool[][],
        m : Int,
        numSwapBits : Int,
        numSelectStates : Int,
    ) : Bool[][] {
        let numChunks = 1 <<< numSwapBits;
        let unreachable = [false, size = m * numChunks];
        mutable table : Bool[][] = [];
        for swap in 0..numChunks - 1 {
            let permutation = SwapNetworkPermutation(numSwapBits, swap);
            for index in 0..numSelectStates - 1 {
                if index < Length(flatData) {
                    let chunks = Chunks(m, flatData[index]);
                    mutable permuted : Bool[] = [];
                    for position in 0..numChunks - 1 {
                        set permuted += chunks[permutation[position]];
                    }
                    set table += [permuted];
                } else {
                    set table += [unreachable];
                }
            }
        }
        table
    }

    internal function DimensionsForSelect(data : Bool[][], address : Qubit[]) : Int {
        let N = Length(data);
        Fact(N > 0, "data cannot be empty");

        let n = Ceiling(Lg(IntAsDouble(N)));
        Fact(Length(address) >= n, $"address register is too small, requires at least {n} qubits");

        return n;
    }

    internal function CreatePaddedData(data : Bool[][], nRequired : Int, m : Int, k : Int) : Bool[][] {
        let dataPadded = Padded(-2^nRequired, [false, size = m], data);

        MappedOverRange(i -> Flattened(dataPadded[i..2^k..2^nRequired - 1]), 0..2^k - 1)
    }

    /// Concatenates the per-outer-index padded lookup tables into one row-major table, so a
    /// combined `(innerSelect, outer)` address indexes it directly.
    internal function FlattenPaddedData(
        data : Bool[][][],
        nRequired : Int,
        m : Int,
        k : Int,
        trimToValidOuter : Bool,
    ) : Bool[][] {
        let numOuterStates = if trimToValidOuter { Length(data) } else { 1 <<< AddressQubits(Length(data)) };
        Flattened(
            MappedOverRange(
                state -> CreatePaddedData(data[UnaryIterationActionIndex(Length(data), state)], nRequired, m, k),
                0..numOuterStates - 1
            )
        )
    }

    //  1D SELECT-SWAP

    internal operation SwapDataOutputs(address : Qubit[], outputs : Qubit[][]) : Unit is Adj {
        let l = Length(address);
        for (i, control) in Enumerated(address) {
            let innerStepSize = 2^i;
            let outerStepSize = 2^(i + 1);
            let numSwaps = 2^l / 2^(i + 1);
            for j in 0..numSwaps - 1 {
                let targets1 = outputs[j * outerStepSize];
                let targets2 = outputs[j * outerStepSize + innerStepSize];
                ApplyToEachA(ts => Controlled SWAP([control], ts), Zipped(targets1, targets2));
            }
        }
    }

    /// Toffoli cost of erasing an `addressBits`-wide load by measurement and phase fixup.
    ///
    /// The `O(sqrt(N))` unlookup: measure the loaded word out in the X basis and repair the
    /// phase it leaves on the address with a lookup over a table of parities.
    internal function MeasurementUnlookupCost(addressBits : Int) : Int {
        2^((addressBits + 1) / 2) + 2^(addressBits / 2) - (addressBits + 2)
    }
}
